Version 1
SubGoalCombiner SGC_AND
INITSECTION
// One custom perform of The Weeping Dawn, would love to add more but not many sound events that can be re-used
DB_SpecialPerformance_EmbersLute("Bard_Perform_WeepingDawn","SE_DEN_TieflingBard_Bard","SE_DEN_TieflingBard_Bard_Stop","EMLUTE_PERFORM_WEEPINGDAWN",169500);
DB_SpecialPerformance_EmbersLute("Bard_Perform_HarpySong","SE_DEN_Harpy_Music","","EMLUTE_PERFORM_HARPYSONG",129900);
DB_SpecialPerformance_EmbersLute("Bard_Perform_WaterQueen","SE_CTY_WaterQueensHouse_Music","","EMLUTE_PERFORM_WATERQUEEN",1950600);
DB_SpecialPerformance_EmbersLute("Bard_Perform_EndlessSpring","EPI_BardScripted_Endless_Spring","","EMLUTE_PERFORM_ENDLESSSPRING",282500);
DB_SpecialPerformance_EmbersLute("Bard_Perform_FinalAct","RaphaelsFinalAct","","EMLUTE_PERFORM_FINALACT",282500);

// Attach music resources to a temporary helper item
DB_SpecialPerformance_Helper_EmbersLute((ITEMROOT)Emlute_Ring_Music_Helper_3debbfbe-e1ef-4067-b66f-4732146fd6bf);
DB_SpecialPerformance_Mapping_EmbersLute("Bard_Perform_WeepingDawn",(GUIDSTRING)SE_DEN_TieflingBard_Bard_a01d680e-75ff-e25b-a854-b633b01dc4e3);
DB_SpecialPerformance_Mapping_EmbersLute("Bard_Perform_HarpySong",(GUIDSTRING)SE_DEN_Harpy_Music_06852a82-79e4-616c-3bb9-b91e029e7d2f);
DB_SpecialPerformance_Mapping_EmbersLute("Bard_Perform_HarpySong",(GUIDSTRING)SE_DEN_Harpy_Music_Close_Enter_603f95a8-99da-6ada-45f8-cf45f7931033);
DB_SpecialPerformance_Mapping_EmbersLute("Bard_Perform_WaterQueen",(GUIDSTRING)SE_CTY_WaterQueensHouse_Music_065e0cda-6a6b-42e1-a1a7-ccd1ca1b793e);
DB_SpecialPerformance_Mapping_EmbersLute("Bard_Perform_WaterQueen",(GUIDSTRING)SE_CTY_WaterQueensHouse_Music_Close_Enter_ed771ea8-11c4-470b-80fb-231291e4f74f);
DB_SpecialPerformance_Mapping_EmbersLute("Bard_Perform_EndlessSpring",(GUIDSTRING)EPI_BardScripted_On_8e7fd934-1d71-4820-ad3d-4d918c79b6c9);
DB_SpecialPerformance_Mapping_EmbersLute("Bard_Perform_EndlessSpring",(GUIDSTRING)EPI_BardScripted_Start_d1e077c1-a18d-4711-8211-6b866824d693);
DB_SpecialPerformance_Mapping_EmbersLute("Bard_Perform_EndlessSpring",(GUIDSTRING)EPI_BardScripted_Endless_Spring_5d8656f7-b4c5-4c2a-88f2-f3a5528b5691);
//DB_SpecialPerformance_Mapping_EmbersLute("Bard_Perform_FinalAct",(GUIDSTRING)Explo_CTY_HouseOfHope_intro_2d72e1a7-c8bd-4508-99b9-9729b7183015);
DB_SpecialPerformance_Mapping_EmbersLute("Bard_Perform_FinalAct",(GUIDSTRING)RaphaelsFinalAct_32bb5df9-997f-428d-95de-a6d425210f08);
DB_SpecialPerformance_Mapping_EmbersLute("Bard_Perform_FinalAct",(GUIDSTRING)Fight_Set_Raphael_On_ff12530d-562a-4555-832d-a501069cdf0b);
DB_SpecialPerformance_Mapping_EmbersLute("Bard_Perform_FinalAct",(GUIDSTRING)Music_Fight_Drive_On_51280667-3665-446f-8c87-4345a9503cb8);
DB_SpecialPerformance_Mapping_EmbersLute("Bard_Perform_FinalAct",(GUIDSTRING)Music_Fight_Resume_9d3ce0e4-8511-56a8-7273-d31b48a4c047);

// Some music events need to be steered through a sequence to get back to the music game state
DB_SpecialPerformance_Mapping_Off_EmbersLute("Bard_Perform_EndlessSpring",(GUIDSTRING)EPI_BardScripted_Off_65724098-1292-43af-a4b2-d07a9063af48);
DB_SpecialPerformance_Mapping_Off_EmbersLute("Bard_Perform_EndlessSpring",(GUIDSTRING)Music_Game_Enter_de4fd7c3-b698-a8b6-959f-66db92ea9540);
DB_SpecialPerformance_Mapping_Off_EmbersLute("Bard_Perform_FinalAct",(GUIDSTRING)Music_Fight_End_85bdedd6-2e60-2e8c-0b31-86f8d25ac65c);

// Don't use this one - entering music event for the credits menu disables all the voice, and I dunno how to put it back!!
DB_SpecialPerformance_EmbersLute("Bard_Perform_ThePowerVocals","Credits_menu_Enter","","EMLUTE_PERFORM_THEPOWERVOCALS",230500);
DB_SpecialPerformance_Mapping_EmbersLute("Bard_Perform_ThePowerVocals",(GUIDSTRING)Credits_menu_Enter_2cf5956c-bf2f-6284-31b3-22382296b982);
DB_SpecialPerformance_Mapping_Off_EmbersLute("Bard_Perform_ThePowerVocals",(GUIDSTRING)Music_Game_Enter_de4fd7c3-b698-a8b6-959f-66db92ea9540);

DB_SpecialPerformance_SummonTemplate_EmbersLute((CHARACTERROOT)Emlute_TieflingBard_Spirit_43d08735-7bb2-48a4-a446-b8814d4409c6,"Alfira");

// Move the tiefling spirit a little bit away from the player when she spawns in
DB_RepositionSummon_EmbersLute((CHARACTERROOT)Emlute_TieflingBard_Spirit_43d08735-7bb2-48a4-a446-b8814d4409c6,1.5,0.0,0.0,1);
// Spawn the Vicious Rockery gnomes in ensemble formation
DB_RepositionSummon_EmbersLute((CHARACTERROOT)Emlute_Drum_Spirit_Gnome_2507263a-a2c8-495f-b485-be77566ba1ff,2.5,0.0,0.0,0);
DB_RepositionSummon_EmbersLute((CHARACTERROOT)Emlute_Flute_Spirit_Gnome_7f8ae293-5f9b-41ed-bad4-d113b0058ed0,-2.5,0.0,0.0,0);
DB_RepositionSummon_EmbersLute((CHARACTERROOT)Emlute_Lute_Spirit_Gnome_a37df723-3ce0-436d-9356-f2d60c395ed0,1.5,0.0,-1.0,0);
DB_RepositionSummon_EmbersLute((CHARACTERROOT)Emlute_Lyre_Spirit_Gnome_4118f9af-a8db-4a17-a785-eec7faadb2df,-1.5,0.0,-1.0,0);
DB_RepositionSummon_EmbersLute((CHARACTERROOT)Emlute_Violin_Spirit_Gnome_36f2416e-abb4-49af-94a8-855b95927e3f,0.0,0.0,-2.5,0);
// Spawn the Rolling Bones in ensemble formation
DB_RepositionSummon_EmbersLute((CHARACTERROOT)Emlute_Drum_Spirit_Skelly_149fd908-e28c-4bb5-b158-774abc383f02,2.0,0.0,0.0,0);
DB_RepositionSummon_EmbersLute((CHARACTERROOT)Emlute_Flute_Spirit_Skelly_4a91d0fc-cf88-4e7d-b110-ae3b205ad90d,-2.0,0.0,0.0,0);
DB_RepositionSummon_EmbersLute((CHARACTERROOT)Emlute_Lute_Spirit_Skelly_9525c5df-6899-413a-8ead-9d5ed865c69e,0.0,0.0,0.0,0);
DB_RepositionSummon_EmbersLute((CHARACTERROOT)Emlute_Lyre_Spirit_Skelly_a2c49c2d-7c98-4313-8ee2-d50cf8999718,0.0,0.0,-2.0,0);
DB_RepositionSummon_EmbersLute((CHARACTERROOT)Emlute_Violin_Spirit_Skelly_e009d828-782d-4e85-a55c-cf088065728b,0.0,0.0,2.0,0);

// Found a weird issue with Countercharms in Patch7: they play silent music when they are summoned in.
// My theory is something to do with trying to start a main performer event before the character spawning is complete?
// Issue seems to affect vanilla Countercharm as well as my custom variant, though I'm going to leave vanilla alone since
// my "fix" is horrible (basically adds a delay before applying the aura status with music performance).
DB_CountercharmFix_EmbersLute((CHARACTERROOT)EMLUTE_Countercharm_MusicalSprite_Drum_ed79a011-50f7-4ce1-be1e-c91d6b5f61e6,"EmbersLuteCountercharmDrum",3500,"EMLUTE_COUNTERCHARM_MUSIC");
DB_CountercharmFix_EmbersLute((CHARACTERROOT)EMLUTE_Countercharm_MusicalSprite_Lyre_f0f9422a-41cf-46bb-9ade-af9aa92fa652,"EmbersLuteCountercharmFlute",3600,"EMLUTE_COUNTERCHARM_MUSIC");
DB_CountercharmFix_EmbersLute((CHARACTERROOT)EMLUTE_Countercharm_MusicalSprite_Violin_913c7b82-cd4c-4bd4-b354-69fde9692684,"EmbersLuteCountercharmMusicBox",3600,"EMLUTE_COUNTERCHARM_MUSIC");
KBSECTION
// Catch the special music performances and trigger the music events.
// Timers are used to automatically stop the performance at the end 
// of the song. Unlike the standard music performances, we won't loop.

IF
MainPerformerStarted((CHARACTER)_Performer,(STRING)_Song)
AND
DB_SpecialPerformance_EmbersLute(_Song,_StartEvent,_,_,_Duration)
THEN
PROC_DebugMusicState(_Performer);
PROC_StartSpecialPerformance_EmbersLute(_Performer,_Song,_StartEvent,_Duration);

IF
StatusRemoved((CHARACTER)_Performer,_Status,_,_)
AND
DB_ActiveSpecialPerformance_EmbersLute(_Performer,_Song,_HelperItem)
AND
DB_SpecialPerformance_EmbersLute(_Song,_,_StopEvent,_Status,_)
THEN
ObjectTimerCancel(_Performer,_Song);
PROC_StopSpecialPerformance_EmbersLute(_Performer,_Song,_StopEvent);

IF
ObjectTimerFinished((CHARACTER)_Performer,_TimerName)
AND
DB_ActiveSpecialPerformance_EmbersLute(_Performer,_TimerName,_HelperItem)
AND
DB_SpecialPerformance_EmbersLute(_TimerName,_,_,_Status,_)
THEN
RemoveStatus(_Performer,_Status);

IF
StatusApplied((CHARACTER)_TieflingBard,"EMLUTE_PERFORM_WEEPINGDAWN_NO_AURA",_,_)
AND
GetTemplate(_TieflingBard,(CHARACTERROOT)_TieflingBardTemplate)
AND
DB_SpecialPerformance_SummonTemplate_EmbersLute(_TieflingBardTemplate,"Alfira")
THEN
DebugText(_TieflingBard,"Playing Weeping Dawn anim");
PlayAnimation(_TieflingBard,DFLT_SCENE_DEN_TieflingBard_Bard_Bard_SongSequence_1981a2b8-a541-148d-d915-89452d4bc108,"");

// Reposition a character on joining party (summon formation).
// Make them face the summon owner (lead performer).
IF
CharacterJoinedParty((CHARACTER)_Summon)
AND
GetTemplate(_Summon,(CHARACTERROOT)_SummonTemplate)
AND
DB_RepositionSummon_EmbersLute(_SummonTemplate,_OffsetX,_OffsetY,_OffsetZ,_)
THEN
PROC_RepositionSummon_EmbersLute(_Summon,_OffsetX,_OffsetY,_OffsetZ);

IF
Teleported((CHARACTER)_Summon,_,_,_,_,_,_,_,_)
AND
GetTemplate(_Summon,(CHARACTERROOT)_SummonTemplate)
AND
DB_RepositionSummon_EmbersLute(_SummonTemplate,_,_,_,1)
AND
CharacterGetOwner(_Summon,_Owner)
THEN
LookAtEntity(_Summon,_Owner);

// "Fix" the music on Improved Countercharm summons by adding a short delay.
// Feels a bit hacky. Seems to work though. 
IF
EnteredLevel((CHARACTER)_Summon,(CHARACTERROOT)_Template, _)
AND
DB_CountercharmFix_EmbersLute(_Template,_TimerName,_Duration,_)
THEN
ObjectTimerCancel(_Summon,_TimerName);
ObjectTimerLaunch(_Summon,_TimerName,_Duration);

IF
ObjectTimerFinished((CHARACTER)_Summon,_TimerName)
AND
DB_CountercharmFix_EmbersLute(_,_TimerName,_,_Status)
THEN
DebugText(_Summon, "Debug: Starting countercharm music");
ApplyStatus(_Summon,_Status,-1.0);



// PROCS and QUERIES
// Starts music on the lead performer, caches active performance and begins a timer
PROC
PROC_StartSpecialPerformance_EmbersLute((CHARACTER)_Performer,(STRING)_Song,(STRING)_StartEvent,(INTEGER)_Duration)
AND
NOT DB_ActiveSpecialPerformance_EmbersLute(_Performer,_Song,_)
AND
DB_SpecialPerformance_Helper_EmbersLute(_HelperTemplate)
AND
CreateAtObject(_HelperTemplate,_Performer,0,0,"",0,_HelperItem)
THEN
DB_ActiveSpecialPerformance_EmbersLute((CHARACTER)_Performer,(STRING)_Song,(ITEM)_HelperItem);
ObjectTimerCancel(_Performer,_Song);
ObjectTimerLaunch(_Performer,_Song,_Duration);
PROC_PlayMusicOnItem(_Song,_HelperItem);

PROC
PROC_PlayMusicOnItem((STRING)_Song,(ITEM)_HelperItem)
AND 
DB_SpecialPerformance_Mapping_EmbersLute(_Song,_MusicSoundResource)
THEN
PlaySoundResource(_HelperItem,_MusicSoundResource);

// Stops music on the lead performer and clears active performance cache
PROC
PROC_StopSpecialPerformance_EmbersLute((CHARACTER)_Performer,(STRING)_Song,(STRING)_StopEvent)
AND
DB_ActiveSpecialPerformance_EmbersLute(_Performer,_Song,_HelperItem)
THEN
PROC_DebugMusicState(_Performer);
PROC_StopMusicOnItem(_Song,_HelperItem);
NOT DB_ActiveSpecialPerformance_EmbersLute(_Performer,_Song,_HelperItem);
ObjectTimerCancel(_HelperItem,"Emlute_DeleteHelperItem");
ObjectTimerLaunch(_HelperItem,"Emlute_DeleteHelperItem",1000);

PROC
PROC_StopMusicOnItem((STRING)_Song,(ITEM)_HelperItem)
AND 
DB_SpecialPerformance_Mapping_Off_EmbersLute(_Song,_MusicSoundResource)
THEN
PlaySoundResource(_HelperItem,_MusicSoundResource);

IF
ObjectTimerFinished((ITEM)_HelperItem,"Emlute_DeleteHelperItem")
THEN
RequestDelete(_HelperItem);

PROC
PROC_RestoreMusicState()
AND 
DB_Audio_CurrentMusicState(_RestoreMusic)
THEN
MusicPlayGeneral(_RestoreMusic);

PROC
PROC_DebugMusicState((CHARACTER)_Performer)
AND 
DB_Audio_CurrentMusicState(_RestoreMusic)
AND
Concatenate("Current music state ",_RestoreMusic,_RestoreMessage)
THEN
DebugText(_Performer,_RestoreMessage);

// Teleports a character to a relative offset position
PROC
PROC_RepositionSummon_EmbersLute((CHARACTER)_Summon,(REAL)_OffsetX,(REAL)_OffsetY,(REAL)_OffsetZ)
AND
GetPosition(_Summon,_X,_Y,_Z)
AND
RealSum(_X,_OffsetX,_NewX)
AND
RealSum(_Y,_OffsetY,_NewY)
AND
RealSum(_Z,_OffsetZ,_NewZ)
THEN
PROC_StopFollow(_Summon);
PROC_TeleportToSafePosition(_Summon,_NewX,_NewY,_NewZ,"",0,0,0,0,1);
EXITSECTION

ENDEXITSECTION
