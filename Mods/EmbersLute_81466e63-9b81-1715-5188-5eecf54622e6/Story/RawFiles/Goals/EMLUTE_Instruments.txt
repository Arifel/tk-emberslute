Version 1
SubGoalCombiner SGC_AND
INITSECTION
// This new Goal is primarily a patch 8 workaround, because the toolbar icons for toggles now default to the Equipment icon instead of the Passive icon. 
// Unsure if this was an intentional change, but for whatever reason, applying Passives directly or via Status preserves the Passive icon. 
// So we do that now *shrug*. Otherwise the toolbar looks stupid with 5+ identical passive toggle icons that do different things.

// Unfortunately, using Statuses instead of the Equipment is horrible to manage passive toggles. StatusOnEquip is a disaster.
// It's fine, until the player quick-swaps instruments, and then all the status add and remove events trip over each other.
// This event firing mess then results in Passive toggles that are just flat broken on the toolbar. Lovely.
// So, to avoid this, I go to our old friend timers.

// Every time I resort to timers, a thousand kittens cry. Forgive me.
DB_RemovePassiveTimer_EmbersLute("MusicalConductor","Emlute_RemoveConductorPassiveToggles",3000);
DB_RemovePassiveTimer_EmbersLute("SpriteSummoning","Emlute_RemoveSpriteSummoningPassiveToggles",2000);

// These instrument modes have custom summoning spells
DB_SummoningInstruments_EmbersLute((TAG)EMLUTE_SPIRIT_MODE_d90f40cc-8091-4807-961c-b3b1e225ad95);
DB_SummoningInstruments_EmbersLute((TAG)EMLUTE_GNOME_MODE_ac260048-1979-4af5-8ebc-37bab10e321b);

// These instrument modes summon sprites
DB_SpriteSummoningInstruments_EmbersLute((TAG)EMLUTE_SPIRIT_MODE_d90f40cc-8091-4807-961c-b3b1e225ad95);
// Which instrument sprite summoning passive toggles to enable by instrument mode equipped
DB_SpriteSummonModePassives_EmbersLute((TAG)EMLUTE_SPIRIT_MODE_d90f40cc-8091-4807-961c-b3b1e225ad95,"Emlute_SummonOnPerform",1);
DB_SpriteSummonModePassives_EmbersLute((TAG)EMLUTE_SPIRIT_MODE_d90f40cc-8091-4807-961c-b3b1e225ad95,"Emlute_SummonOnPerform_TieflingBard",1);
DB_SpriteSummonModePassives_EmbersLute((TAG)EMLUTE_SPIRIT_MODE_d90f40cc-8091-4807-961c-b3b1e225ad95,"Emlute_SummonOnPerform_Vocal",1);

// These instrument modes grant the Musical Conductor passive
DB_ConductorInstruments_EmbersLute((TAG)EMLUTE_SPIRIT_MODE_d90f40cc-8091-4807-961c-b3b1e225ad95);
DB_ConductorInstruments_EmbersLute((TAG)EMLUTE_GNOME_MODE_ac260048-1979-4af5-8ebc-37bab10e321b);
// Which instrument conductor passive toggles to enable by instrument type equipped
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_LUTE_3a9a899b-4177-4763-b329-2598528beca5,"Emlute_ViolinsToggle",1);
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_LUTE_3a9a899b-4177-4763-b329-2598528beca5,"Emlute_LyresToggle",1);
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_LUTE_3a9a899b-4177-4763-b329-2598528beca5,"Emlute_DrumsToggle",1);
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_LUTE_3a9a899b-4177-4763-b329-2598528beca5,"Emlute_FlutesToggle",1);
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_LUTE_3a9a899b-4177-4763-b329-2598528beca5,"Emlute_LutesToggle",0);
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_VIOLIN_81dbb2dd-ffe9-4b6a-9159-2c32c889e023,"Emlute_LutesToggle",1);
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_VIOLIN_81dbb2dd-ffe9-4b6a-9159-2c32c889e023,"Emlute_LyresToggle",1);
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_VIOLIN_81dbb2dd-ffe9-4b6a-9159-2c32c889e023,"Emlute_DrumsToggle",1);
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_VIOLIN_81dbb2dd-ffe9-4b6a-9159-2c32c889e023,"Emlute_FlutesToggle",1);
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_VIOLIN_81dbb2dd-ffe9-4b6a-9159-2c32c889e023,"Emlute_ViolinsToggle",0);
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_FLUTE_838663ea-de99-44a4-bc4c-13ed5a6d75e9,"Emlute_LutesToggle",1);
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_FLUTE_838663ea-de99-44a4-bc4c-13ed5a6d75e9,"Emlute_LyresToggle",1);
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_FLUTE_838663ea-de99-44a4-bc4c-13ed5a6d75e9,"Emlute_DrumsToggle",1);
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_FLUTE_838663ea-de99-44a4-bc4c-13ed5a6d75e9,"Emlute_ViolinsToggle",1);
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_FLUTE_838663ea-de99-44a4-bc4c-13ed5a6d75e9,"Emlute_FlutesToggle",0);
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_LYRE_cd85b343-55a9-4b66-bd92-42ca70a084aa,"Emlute_LutesToggle",1);
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_LYRE_cd85b343-55a9-4b66-bd92-42ca70a084aa,"Emlute_FlutesToggle",1);
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_LYRE_cd85b343-55a9-4b66-bd92-42ca70a084aa,"Emlute_DrumsToggle",1);
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_LYRE_cd85b343-55a9-4b66-bd92-42ca70a084aa,"Emlute_ViolinsToggle",1);
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_LYRE_cd85b343-55a9-4b66-bd92-42ca70a084aa,"Emlute_LyresToggle",0);
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_DRUM_f3f2d52d-9ef3-492c-a7ec-d9b35a130c05,"Emlute_LutesToggle",1);
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_DRUM_f3f2d52d-9ef3-492c-a7ec-d9b35a130c05,"Emlute_FlutesToggle",1);
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_DRUM_f3f2d52d-9ef3-492c-a7ec-d9b35a130c05,"Emlute_LyresToggle",1);
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_DRUM_f3f2d52d-9ef3-492c-a7ec-d9b35a130c05,"Emlute_ViolinsToggle",1);
DB_ConductorPassives_EmbersLute((TAG)MUSIC_VFX_DRUM_f3f2d52d-9ef3-492c-a7ec-d9b35a130c05,"Emlute_DrumsToggle",0);
KBSECTION
// Any player equips any Ember's Lute mod instrument which grants Musical Conductor.
// Add and Remove the Musical Conductor passive toggles based on the instrument type.
IF
Equipped(_Instrument, _Character)
AND
DB_Players(_Character)
AND
DB_ConductorInstruments_EmbersLute(_Tag)
AND
IsTagged(_Instrument, _Tag, 1)
THEN
PROC_Emlute_AddRemoveConductorPassives(_Character, _Instrument);

// Any player equips any Ember's Lute mod instrument which summons musical sprites.
// Add and Remove the sprite summoning passive toggles.
IF
Equipped(_Instrument, _Character)
AND
DB_Players(_Character)
AND
DB_SpriteSummoningInstruments_EmbersLute(_Tag)
AND
IsTagged(_Instrument, _Tag, 1)
THEN
PROC_Emlute_AddRemoveSpriteSummoningPassives(_Character, _Instrument);

// Any player unequips any Ember's Lute mod instrument.
// Spike 1 turn of a varnish status overwrite to ensure it gets removed.
IF
Unequipped(_Instrument, _Character)
AND
DB_Players(_Character)
AND
QRY_IsEmbersLuteInstrument(_Instrument)
THEN
ApplyStatus(_Instrument,"EMLUTE_VARNISH_INSTRUMENT_CLEAR",1.0);

// Any player unequips any Ember's Lute mod instrument with summoning abilities.
// Always call the Dismiss spell to clear up any summons.
IF
Unequipped(_Instrument, _Character)
AND
DB_Players(_Character)
AND
QRY_IsEmbersLuteSummoningInstrument(_Instrument)
THEN
UseSpell(_Character,"Shout_Dismiss_All_MusicalSpirits",_Character);

// Any player unequips an Ember's Lute mod instrument which grants Musical Conductor.
// Wait 3 seconds, then clear up any passive toggles no longer required.
IF
Unequipped(_Instrument, _Character)
AND
DB_Players(_Character)
AND
DB_ConductorInstruments_EmbersLute(_Tag)
AND
IsTagged(_Instrument, _Tag, 1)
AND
DB_RemovePassiveTimer_EmbersLute("MusicalConductor",_TimerName,_TimerDuration)
THEN
ObjectTimerCancel(_Character,_TimerName);
ObjectTimerLaunch(_Character,_TimerName,_TimerDuration);

// Remove all the conductor passives no longer required.
// But abort if player still has MusicalConductor passive.
IF
ObjectTimerFinished((CHARACTER)_Character, _TimerName)
AND
DB_RemovePassiveTimer_EmbersLute("MusicalConductor",_TimerName, _TimerDuration)
AND
HasPassive(_Character, "Emlute_MusicalConductor",0)
THEN
PROC_Emlute_RemoveAllConductorPassives(_Character);

// Any player unequips an Ember's Lute mod instrument which summons sprites.
// Wait 3 seconds, then clear up any passive toggles no longer required.
IF
Unequipped(_Instrument, _Character)
AND
DB_Players(_Character)
AND
DB_SpriteSummoningInstruments_EmbersLute(_Tag)
AND
IsTagged(_Instrument, _Tag, 1)
AND
DB_RemovePassiveTimer_EmbersLute("SpriteSummoning",_TimerName,_TimerDuration)
THEN
ObjectTimerCancel(_Character,_TimerName);
ObjectTimerLaunch(_Character,_TimerName,_TimerDuration);

// Remove all the conductor passives no longer required.
// But abort if player still has a sprite summoning instrument equipped.
IF
ObjectTimerFinished((CHARACTER)_Character, _TimerName)
AND
DB_RemovePassiveTimer_EmbersLute("SpriteSummoning",_TimerName, _TimerDuration)
AND
NOT QRY_IsEmbersLuteSpriteSummoningInstrumentEquipped(_Character)
THEN
PROC_Emlute_RemoveAllSpriteSummoningPassives(_Character);

PROC
PROC_Emlute_RemoveAllConductorPassives((CHARACTER)_Character)
AND
DB_ConductorPassives_EmbersLute(_InstrumentTypeTag, _Passive, _Any)
AND
HasPassive(_Character,_Passive,1)
THEN
RemovePassive(_Character, _Passive);

PROC
PROC_Emlute_RemoveAllSpriteSummoningPassives((CHARACTER)_Character)
AND
DB_SpriteSummonModePassives_EmbersLute(_InstrumentTypeTag, _Passive, _Any)
AND
HasPassive(_Character,_Passive,1)
THEN
RemovePassive(_Character, _Passive);

PROC
PROC_Emlute_AddRemoveConductorPassives((CHARACTER)_Character, (ITEM)_Instrument)
AND
DB_ConductorPassives_EmbersLute(_InstrumentTypeTag, _Passive, 0)
AND
IsTagged(_Instrument, _InstrumentTypeTag,1)
AND
HasPassive(_Character,_Passive,1)
THEN
RemovePassive(_Character, _Passive);

PROC
PROC_Emlute_AddRemoveConductorPassives((CHARACTER)_Character, (ITEM)_Instrument)
AND
DB_ConductorPassives_EmbersLute(_InstrumentTypeTag, _Passive, 1)
AND
IsTagged(_Instrument, _InstrumentTypeTag,1)
AND
HasPassive(_Character,_Passive,0)
THEN
AddPassive(_Character, _Passive);

PROC
PROC_Emlute_AddRemoveSpriteSummoningPassives((CHARACTER)_Character, (ITEM)_Instrument)
AND
DB_SpriteSummonModePassives_EmbersLute(_InstrumentTypeTag, _Passive, 1)
AND
IsTagged(_Instrument, _InstrumentTypeTag,1)
AND
HasPassive(_Character,_Passive,0)
THEN
AddPassive(_Character, _Passive);

QRY
QRY_IsEmbersLuteInstrument((ITEM)_Instrument)
AND
DB_DLC_DivinitySongPack_InstrumentTags_EmbersLute(_Tag)
AND
IsTagged(_Instrument, _Tag, 1)
THEN
DB_NOOP(1);

QRY
QRY_IsEmbersLuteSummoningInstrument((ITEM)_Instrument)
AND
DB_SummoningInstruments_EmbersLute(_Tag)
AND
IsTagged(_Instrument,_Tag,1)
THEN
DB_NOOP(1);

QRY
QRY_IsEmbersLuteSpriteSummoningInstrumentEquipped((CHARACTER)_Character)
AND
GetEquippedItem(_Character, "MusicalInstrument", _Instrument)
AND
DB_SpriteSummoningInstruments_EmbersLute(_Tag)
AND
IsTagged(_Instrument,_Tag,1)
THEN
DB_NOOP(1);




EXITSECTION

ENDEXITSECTION
